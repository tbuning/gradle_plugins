apply plugin: 'maven-publish'

publishing {
    publications {
        aar(MavenPublication) {
            groupId ARTIFACT_PACKAGE
            version = ARTIFACT_VERSION
            artifactId ARTIFACT_NAME

            // Tell maven to prepare the generated "*.aar" file for publishing
            artifact("$buildDir/outputs/aar/${project.name}-release.aar")

            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.getByName("_releaseCompile").getResolvedConfiguration().getFirstLevelModuleDependencies().each {
                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.moduleGroup)
                    dependency.appendNode('artifactId', it.moduleName)
                    dependency.appendNode('version', it.moduleVersion)
                }
            }
        }
    }
}

publishing {
    repositories {
        maven {
            name 'Release'
            url "$NEXUS_URL/proape-releases"
            credentials {
                username "$NEXUS_USER"
                password "$NEXUS_PASSWORD"
            }
        }
        maven {
            name 'Snapshots'
            url "$NEXUS_URL/proape-snapshots"
            credentials {
                username "$NEXUS_USER"
                password "$NEXUS_PASSWORD"
            }
        }
    }
}
